<html>
<head>
<title>DarkBASIC Professional Help File</title>
</head>
<body background="..\..\gfx\dbpro_bg.jpg">
<!-- Page Header -->
<center><table width="340" border="0" cellpadding="0" cellspacing="0">
<tr>
	<td><img src="..\..\gfx\dbph_head_1.jpg" width="102" height="51"></td>
	<td><a href="..\..\main.htm"><img src="..\..\gfx\dbph_head_2.jpg" width="47" height="51" border="0"></a></td>
	<td><a href="..\..\commands.htm"><img src="..\..\gfx\dbph_head_3.jpg" width="50" height="51" border="0"></a></td>
	<td><a href="..\..\examples.htm"><img src="..\..\gfx\dbph_head_4.jpg" width="47" height="51" border="0"></a></td>
	<td><a href="..\..\documents.htm"><img src="..\..\gfx\dbph_head_5.jpg" width="46" height="51" border="0"></a></td>
	<td><a href="..\..\index.htm"><img src="..\..\gfx\dbph_head_6.jpg" width="56" height="51" border="0"></a></td>
</tr>
</table></center>
<font face="Verdana">
<table width="100%" border="0" cellpadding="0" cellspacing="0">
<tr><td>
<BR><BR><pre><FONT FACE=Courier SIZE=2>rem Terrain Functionality

rem Standard Setup Code for all examples
sync on : sync rate 0 : color backdrop 0
set text font "arial" : set text size 16
set text to bold : set text transparent

rem Make Simple Terrain
TerrainNumber=1
make terrain TerrainNumber,"land.bmp"

rem Position The Terrain
position terrain TerrainNumber, 0, 0, 1025

rem Texture terrain
ImageNumber=1
load image "grass.bmp",ImageNumber
texture terrain TerrainNumber,ImageNumber
size#=1025

rem Load sky object
load object "sky\sb.x",1
scale object 1,40,40,40
set object cull 1,0
set object light 1,0
set object texture 1,2,1

rem Position camera in center of terrain
position camera 512,10,512

rem Main loop
desc$="LOD Terrain from Bitmap"
while mouseclick()=0

rem Control camera over terrain
control camera using arrowkeys 0,1,1
cx#=camera position x(0)
cz#=camera position z(0)
if cx#<0.0 then cx#=0
if cx#>size# then cx#=size#
if cz#<0.0 then cz#=0
if cz#>size# then cz#=size#
position camera cx#,get terrain height(TerrainNumber,cx#,cz#)+5,cz#

rem Terrain Data
set cursor 0,0
print "TERRAIN DATA"
print " exist:";terrain exist(TerrainNumber)
if terrain exist(TerrainNumber)=1
 print " x:";terrain position x(TerrainNumber)
 print " y:";terrain position y(TerrainNumber)
 print " z:";terrain position z(TerrainNumber)
 print " height:";get terrain height(TerrainNumber, cx#, cz#)
 print " totalheight:";get total terrain height(TerrainNumber)
endif
print
print "CAMERA DATA"
print " x:";camera position x(0)
print " y:";camera position y(0)
print " z:";camera position z(0)

rem Position sky at player
position object 1,camera position x(0),camera position y(0)-150,camera position z(0)

rem Show Framerate
text 20,screen height()-40,desc$
fps$="DBPro Fps: "+str$(screen fps())
text screen width()-20-text width(fps$),screen height()-40,fps$

rem Update screen
sync

rem End loop
endwhile

rem Delete Terrain
delete terrain TerrainNumber</pre></td></tr></table>
<br>
<!-- Page Footer -->
<table width="100%" border="0" cellpadding="0" cellspacing="0">
<tr>
	<td align="center"><img src="..\..\gfx\dbph_foot_1.jpg" width="340" height="38"></td>
</tr>
</table>
</body>
</html>
