<html>
<head>
<title>DarkBASIC Professional Help File</title>
</head>
<body background="..\..\gfx\dbpro_bg.jpg">
<!-- Page Header -->
<center><table width="340" border="0" cellpadding="0" cellspacing="0">
<tr>
	<td><img src="..\..\gfx\dbph_head_1.jpg" width="102" height="51"></td>
	<td><a href="..\..\main.htm"><img src="..\..\gfx\dbph_head_2.jpg" width="47" height="51" border="0"></a></td>
	<td><a href="..\..\commands.htm"><img src="..\..\gfx\dbph_head_3.jpg" width="50" height="51" border="0"></a></td>
	<td><a href="..\..\examples.htm"><img src="..\..\gfx\dbph_head_4.jpg" width="47" height="51" border="0"></a></td>
	<td><a href="..\..\documents.htm"><img src="..\..\gfx\dbph_head_5.jpg" width="46" height="51" border="0"></a></td>
	<td><a href="..\..\index.htm"><img src="..\..\gfx\dbph_head_6.jpg" width="56" height="51" border="0"></a></td>
</tr>
</table></center>
<font face="Verdana">
<table width="100%" border="0" cellpadding="0" cellspacing="0">
<tr><td>
<BR><BR><pre><FONT FACE=Courier SIZE=2>rem Matrix Showcase

rem Standard Setup Code for all examples
sync on : sync rate 0 : color backdrop 0
set text font "arial" : set text size 16
set text to bold : set text transparent

rem Create matrix
load image "face.bmp",1
load image "water.bmp",2
for m=1 to 2
 if m=1 then s=200
 if m=2 then s=100
 make matrix m,s,s,36,36
 if m=1 then position matrix m,-50,20,-50
 if m=2 then position matrix m,0,120,0
 prepare matrix texture m,m,36,36
 ghost matrix on m
 tc=1
 for z=35 to 0 step -1
  x=0
  while x<=35
   set matrix tile m,x,z,tc
   inc tc : inc x
  endwhile
 next z
next m

rem Setup light
set point light 0,50,500,50
color light 0,1000,1000,200

rem Setup camera
position camera 50,170,50
point camera 50,0,50

rem Main loop
desc$="Water Matrix (Hit Space)"
do

rem Space controls wireframe
if spacekey()=1
 if wr=0
  wrstate=1-wrstate
  if wrstate=0 then set matrix wireframe off 1 : set matrix wireframe off 2
  if wrstate=1 then set matrix wireframe on 1 : set matrix wireframe on 2
  wr=1
 endif
else
 wr=0
endif

rem Control matrix like water
for m=1 to 2
 a#=wrapvalue(a#+1)
 for z=0 to 36
  for x=0 to 36
   x#=cos(a#+(x*10))
   y#=sin(a#+(z*10))
   nx#=0.0-(cos(a#+(x*10))*0.25)
   nz#=0.0-(sin(a#+(z*10))*0.25)
   ny#=1.0-nx#-nz#
   set matrix height m,x,z,(x#+y#)*15.0
   set matrix normal m,x,z,nx#,ny#,nz#
  next x
 next z
 update matrix m
next m

rem Show Framerate
text 20,screen height()-40,desc$
fps$="DBPro Fps: "+str$(screen fps())
text screen width()-20-text width(fps$),screen height()-40,fps$

rem Update screen
sync

rem End loop
loop
</pre></td></tr></table>
<br>
<!-- Page Footer -->
<table width="100%" border="0" cellpadding="0" cellspacing="0">
<tr>
	<td align="center"><img src="..\..\gfx\dbph_foot_1.jpg" width="340" height="38"></td>
</tr>
</table>
</body>
</html>
